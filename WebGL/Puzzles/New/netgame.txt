title Net Game
author polyomino games
homepage polyomino.org

comment //
endline ;

//debug
//verbose_logging

background_color lightblue
text_color darkbrown

run_rules_on_level_start
enable_click
enable_hover
animate_interval 0.2

========
OBJECTS
========

edge *
lightgrey lightblue blue
012012012
120120120
201201201
012012012
120120120
201201201
012012012
120120120
201201201

focus 
grey
00000000
0......0
0......0
0......0
0......0
0......0
0......0
00000000

tileN
red
....0....
....0....
....0....
....0....
....0....
.........
.........
.........
.........

tileE
red
.........
.........
.........
.........
....00000
.........
.........
.........
.........

tileS
red
.........
.........
.........
.........
....0....
....0....
....0....
....0....
....0....


tileW
red
.........
.........
.........
.........
00000....
.........
.........
.........
.........

Background .;green
init;red
mix;blue
lit;lightbrown
done;transparent
start s;transparent
empty e;transparent
temp;transparent
q1;transparent
q2;transparent
q3;transparent
q4;transparent
tileX;transparent  // extra tile for shifts
r0;transparent
r90;transparent
r180;transparent
r270;transparent
c1;transparent
c2;transparent
c3;transparent

=======
LEGEND
=======

i = init and edge
m = mix and edge
tileNESW = tileN or tileE or tileS or tileW
active = start or tileNESW
clickable = tileNESW
// tile rotation
tileRL0 = tileN or tileE or tileS or tileW or tileX
tileRL1 = tileX or tileN or tileE or tileS or tileW
tileRR0 = tileN or tileW or tileS or tileE or tileX
tileRR1 = tileX or tileN or tileW or tileS or tileE
tileRX0 = tileN or tileS or tileX or tileW or tileE or tileX
tileRX1 = tileX or tileN or tileS or tileX or tileW or tileE
queues = temp or q1 or q2 or q3 or q4
rotation = r0 or r90 or r180 or r270
rr = r90 or r180 or r270
tempobj = r0 or init or mix

=======
SOUNDS
=======

clickable click1 32169907 afxRR
clickable click2 32169907 afxRL
sfx0 32169907
sfx1 16334503

================
COLLISIONLAYERS
================
Background
edge 
lit init mix
start
empty
queues
c1, c2, c3
rotation
done
tileN
tileE
tileS
tileW
tileX
focus

======
RULES
======

[done] -> cancel // it's already a win

// grow the tree to the edge
startloop
  random [ init ][ active no c3 no edge | no active no edge ] -> [ init ][ active temp | temp ]
// avoid cross tiles
  [ temp c2 ] -> [ temp c3 ]
  [ temp c1 ] -> [ temp c2 ]
  [ temp no c1 no c2 no c3 ] -> [ temp c1 ]
  right  [ temp | temp ] -> [ tileE | tileW ]
  up     [ temp | temp ] -> [ tileN | tileS ]
endloop 

// scramble the tile rotations 
[ mix ][ tileNESW no rotation ] -> [ mix ][ tileNESW random rotation ]
startloop
  [ mix ][ tileNESW ] -> [ mix ] [ action tileNESW ]
  [ rr action TileN no TileW ] -> [ rr TileW ]
  [ rr action TileE no TileN ] -> [ rr TileN ]
  [ rr action TileS no TileE ] -> [ rr TileE ]
  [ rr action TileW no TileS ] -> [ rr TileS ]
  [ rr action TileN no TileW ] -> [ rr TileW ]
  [ rr action TileE no TileN ] -> [ rr TileN ]
  [ r90 ] -> [ r0 ]
  [ r180 ] -> [ r90 ]
  [ r270 ] -> [ r180 ]
endloop
//[ r0 ] -> []
//[ init ] -> []
//[ mix ] -> []
[ tempobj ] -> []

[ focus ] -> []
[ hover clickable ] -> [ clickable focus ]

[ init ][ tileE | tileE | tileE ] -> [ init ][ click1 tileE | tileE | tileE ]
// accept a user input
  [ click1 tileRL0 ] -> [ click1 tileRL1 ] 
+ [ click1 tileRL0 ] -> [ tileRL0 ]
  [ click2 tileRR0 ] -> [ click2 tileRR1 ] 
+ [ click2 tileRR0 ] -> [ tileRR0 ]

[ lit ] -> []
[ start ] -> [ start lit ]
startloop
  right [ tileE lit | tileW no lit ] -> [ tileE lit | tileW lit ]
  down  [ tileS lit | tileN no lit ] -> [ tileS lit | tileN lit ]
  up    [ tileN lit | tileS no lit ] -> [ tileN lit | tileS lit ]
  left  [ tileW lit | tileE no lit ] -> [ tileW lit | tileE lit ]
endloop

[ start ] -> [ start done ]
[ done ][ clickable no lit ] -> [][ clickable ]
[ done ] -> sfx1
[ done ] -> status You win! Press R to play again, or Esc to change level.

==============
WINCONDITIONS
==============

=======
LEVELS
=======

message The aim is to light up the whole board

level Single start 9x9
i*********m
*.........*
*.........*
*.........*
*.........*
*....s....*
*.........*
*.........*
*.........*
*.........*
***********

level Single start 13x11
i**************
*.............*
*.............*
*.............*
*.............*
*.............*
*......s......*
*.............*
*.............*
*.............*
*.............*
*.............*
***************

// test levels

level Single start 1x1
i*m
*s*
*.*
***

level Single start 3x3 no mix
i****
*...*
*.s.*
*...*
*****

level Single start 3x3
i***m
*...*
*.s.*
*...*
*****

level Single start 5x5
i*****m
*.....*
*.....*
*..s..*
*.....*
*.....*
*******

level Single start 8x8 no mix
i*********
*........*
*........*
*....s...*
*........*
*........*
*........*
*........*
**********

