title Net Game
author polyomino games
homepage polyomino.org

comment //
endline ;

//debug
//verbose_logging

background_color lightblue
text_color darkbrown

run_rules_on_level_start
animate_interval 0.2
clicks
hover

========
OBJECTS
========

edge *
lightgrey lightblue blue
012012012
120120120
201201201
012012012
120120120
201201201
012012012
120120120
201201201

focus 
grey
00000000
0......0
0......0
0......0
0......0
0......0
0......0
00000000

tileN
red
....0....
....0....
....0....
....0....
....0....
.........
.........
.........
.........

tileE
red
.........
.........
.........
.........
....00000
.........
.........
.........
.........

tileS
red
.........
.........
.........
.........
....0....
....0....
....0....
....0....
....0....


tileW
red
.........
.........
.........
.........
00000....
.........
.........
.........
.........

Background .;green
init;red
mix;blue
test;yellow
lit;lightbrown
done;transparent
start s;transparent
empty e;transparent
temp;pink
q1;pink
q2;pink
q3;pink
q4;pink
tileX;pink  // extra tile for shifts
r0;pink
r90;pink
r180;pink
r270;pink
c1;pink
c2;pink
c3;pink

=======
LEGEND
=======

i = init and edge
m = mix and edge
t = test and background
tileNESW = tileN or tileE or tileS or tileW
active = start or tileNESW
//clickable = tileNESW
player = tileNESW
// tile rotation
tileRL0 = tileN or tileE or tileS or tileW or tileX
tileRL1 = tileX or tileN or tileE or tileS or tileW
tileRR0 = tileN or tileW or tileS or tileE or tileX
tileRR1 = tileX or tileN or tileW or tileS or tileE
tileRX0 = tileN or tileS or tileX or tileW or tileE or tileX
tileRX1 = tileX or tileN or tileS or tileX or tileW or tileE
queues = temp or q1 or q2 or q3 or q4
rot4 = r0 or r90 or r180 or r270
rot3 = r90 or r180 or r270
tempobj = r0 or c1 or c2 or c3

=======
SOUNDS
=======

player click1 32169907 afxRR
player click2 32169907 afxRL
sfx0 32169907
sfx1 16334503

================
COLLISIONLAYERS
================
Background
edge 
lit init mix test
start
empty
queues
c1, c2, c3
rot4
done
tileN
tileE
tileS
tileW
tileX
focus

======
RULES
======

[done] -> cancel // it's already a win and we told them
[init] -> [] call setup
[mix]  -> [] call scramble
//[ test clickable ] -> [ click1 clickable ]
[player] -> call userinput
[tempobj] -> []

///////////////////////////////////////////////////////////////////////////////
//--- initial setup
segment setup
  // grow the tree to the edge
  startloop
    random [ active no c3 no edge | no active no edge ] -> [ active temp | temp ]
    // avoid cross tiles
    [ temp c2 ] -> [ temp c3 ]
    [ temp c1 ] -> [ temp c2 ]
    [ temp no c1 no c2 no c3 ] -> [ temp c1 ]
    right  [ temp | temp ] -> [ tileE | tileW ]
    up     [ temp | temp ] -> [ tileN | tileS ]
  endloop 

///////////////////////////////////////////////////////////////////////////////
//--- scramble the tile rotations 
segment scramble
  [ tileNESW no rot4 ] -> [ tileNESW random rot4 ]
  startloop
    [ tileNESW ] -> [ action tileNESW ]
    [ rot3 action TileN no TileW ] -> [ rot3 TileW ]
    [ rot3 action TileE no TileN ] -> [ rot3 TileN ]
    [ rot3 action TileS no TileE ] -> [ rot3 TileE ]
    [ rot3 action TileW no TileS ] -> [ rot3 TileS ]
    [ rot3 action TileN no TileW ] -> [ rot3 TileW ]
    [ rot3 action TileE no TileN ] -> [ rot3 TileN ]
    [ r90 ] -> [ r0 ]
    [ r180 ] -> [ r90 ]
    [ r270 ] -> [ r180 ]
  endloop

///////////////////////////////////////////////////////////////////////////////
// accept a user input

segment userinput

  //[ focus ] -> []
  //[ hover clickable ] -> [ clickable focus ]

  [ click1 tileRL0 ] -> [ click1 tileRL1 ] 
+ [ click1 tileRL0 ] -> [ tileRL0 ]
  [ click2 tileRR0 ] -> [ click2 tileRR1 ] 
+ [ click2 tileRR0 ] -> [ tileRR0 ]

// propagate lit to all connected tiles
  [ lit ] -> []
  [ start ] -> [ start lit ]
  startloop
    right [ tileE lit | tileW no lit ] -> [ tileE lit | tileW lit ]
    down  [ tileS lit | tileN no lit ] -> [ tileS lit | tileN lit ]
    up    [ tileN lit | tileS no lit ] -> [ tileN lit | tileS lit ]
    left  [ tileW lit | tileE no lit ] -> [ tileW lit | tileE lit ]
  endloop

// check for win
  [ start ] -> [ start done ]
  [ done ][ player no lit ] -> [][ player ]
  [ done ] -> sfx1
  [ done ] -> status You win! Press R to play again, or Esc to change level.

==============
WINCONDITIONS
==============

=======
LEVELS
=======

message The aim is to light up the whole board

level Single start 9x9
i*********m
*.........*
*.........*
*.........*
*.........*
*....s....*
*.........*
*.........*
*.........*
*.........*
***********

level Single start 13x11
i*************m
*.............*
*.............*
*.............*
*.............*
*.............*
*......s......*
*.............*
*.............*
*.............*
*.............*
*.............*
***************

// test levels

level Single start 13x11 no mix
i**************
*t............*
*.............*
*.............*
*.............*
*.............*
*......s......*
*.............*
*.............*
*.............*
*.............*
*.............*
***************

level Single start 1x1
i*m
*s*
*.*
***

level Single start 3x3 no mix
i****
*...*
*.s.*
*...*
*****

level Single start 3x3
i***m
*...*
*.s.*
*...*
*****

level Single start 5x5
i*****m
*.....*
*.....*
*..s..*
*.....*
*.....*
*******

level Single start 8x8 no mix
i*********
*........*
*........*
*....s...*
*........*
*........*
*........*
*........*
**********

