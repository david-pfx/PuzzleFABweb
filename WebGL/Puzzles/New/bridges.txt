title Bridges
author polyomino games
homepage polyomino.org

comment //
endline ;

color_palette mastersystem
background_color blue
text_color yellow

noarrows
noaction
clicks

========
OBJECTS
========

edge *
blue green lightblue lightgreen darkgreen darkblue yellow red orange
012345678
123456780
234567801
345678012
456780123
567801234
678012345
780123456
801234567

cell
darkblue green
1110000000111
110.......011
10.........01
0...........0
0...........0
0...........0
0...........0
0...........0
0...........0
0...........0
10.........01
110.......011
1110000000111

// numbers in cells -- macro would help
N1;black;resize 0.7;text 1
N2;black;resize 0.7;text 2
N3;black;resize 0.6;text 3
N4;black;resize 0.6;text 4
N5;black;resize 0.6;text 5
N6;black;resize 0.6;text 6
N7;black;resize 0.7;text 7
N8;black;resize 0.7;text 8

singley
darkblue
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....
....0....

singlex
darkblue
000000000

doubley
darkblue
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...
...0.0...

doublex
darkblue
000000000
.........
000000000

c0;transparent;text A
c1;transparent;text B
c2;transparent;text C
c3;transparent;text D
c4;transparent;text E 
c5;transparent;text F 
c6;transparent;text G
c7;transparent;text H 
c8;transparent;text I

Background .;green
player -;transparent
lit;yellow
win;transparent

=======
LEGEND
=======

number = n1 or n2 or n3 or n4 or n5 or n6 or n7 or n8
1 = n1 and cell
2 = n2 and cell
3 = n3 and cell
4 = n4 and cell
5 = n5 and cell
6 = n6 and cell
7 = n7 and cell
8 = n8 and cell
single = singley or singlex
double = doubley or doublex
linex = singlex or doublex
liney = singley or doubley
line = single or double

// map to add 1 or 2, in reverse order to propagation on rule expansions
count  = c8 or c7 or c6 or c5 or c4 or c3 or c2 or c1 or c0
count1 = c8 or c8 or c7 or c6 or c5 or c4 or c3 or c2 or c1
count2 = c8 or c8 or c8 or c7 or c6 or c5 or c4 or c3 or c2

=======
SOUNDS
=======

sfx1 3622903

================
COLLISIONLAYERS
================
Background
edge 
count lit
player
line
cell
number
win 

======
RULES
======

[ win ] -> cancel // it's already a win and we said so
[ player ] -> call player move
[ player ] -> call show lit
[ player ] -> call check win

////////////////////////////////////////////////////////////////////////////////
segment player move
// single line left right
horizontal [ cell    | click1 player no line ]           -> [ cell    | player action singlex ]
horizontal [ cell    | action singlex | player no line ] -> [ cell    | singlex | player action singlex ]
horizontal [ singlex | action singlex | player no line ] -> [ singlex | singlex | player action singlex ]

// promote single to double
horizontal [ cell    | click1 player singlex ]              -> [ cell    | player action doublex ]
horizontal [ cell    | action doublex | singlex ]           -> [ cell    | doublex | action doublex ]
horizontal [ doublex | action doublex | singlex ]           -> [ doublex | doublex | action doublex ]

// single line up down
vertical   [ cell    | click2 player no line ]           -> [ cell    | player action singley ]
vertical   [ cell    | action singley | player no line ] -> [ cell    | singley | player action singley ]
vertical   [ singley | action singley | player no line ] -> [ singley | singley | player action singley ]

// promote single to double
vertical   [ cell    | click2 player singley ]              -> [ cell    | player action doubley ]
vertical   [ cell    | action doubley | singley ]           -> [ cell    | doubley | action doubley ]
vertical   [ doubley | action doubley | singley ]           -> [ doubley | doubley | action doubley ]

// remove line
horizontal [ cell | click1 player doublex ] -> [ cell | player ]
vertical   [ cell | click2 player doubley ] -> [ cell | player ]

// remove loose ends
horizontal [ linex | player no line ] -> [ | player ]
vertical   [ liney | player no line ] -> [ | player ]

////////////////////////////////////////////////////////////////////////////////
segment show lit

// calculate a count for each cell, using the final prefix so the rule applies only once
            [ cell ] -> [ cell c0 ]
up    final [ cell count | singley ] -> [ cell count1 | singley ]
down  final [ cell count | singley ] -> [ cell count1 | singley ]
left  final [ cell count | singlex ] -> [ cell count1 | singlex ]
right final [ cell count | singlex ] -> [ cell count1 | singlex ]
up    final [ cell count | doubley ] -> [ cell count2 | doubley ]
down  final [ cell count | doubley ] -> [ cell count2 | doubley ]
left  final [ cell count | doublex ] -> [ cell count2 | doublex ]
right final [ cell count | doublex ] -> [ cell count2 | doublex ]

// light up every cell where the count matches
    [ lit ] -> [ ]
    [ 1 c1 ] -> [ 1 lit ]
    [ 2 c2 ] -> [ 2 lit ]
    [ 3 c3 ] -> [ 3 lit ]
    [ 4 c4 ] -> [ 4 lit ]
    [ 5 c5 ] -> [ 5 lit ]
    [ 6 c6 ] -> [ 6 lit ]
    [ 7 c7 ] -> [ 7 lit ]
    [ 8 c8 ] -> [ 8 lit ]

segment check win
[ player ] -> [ player win ]
[ cell no lit ][ player win ] -> [ cell ][ player ]
[ win ] -> sfx1
[ win ] -> status You win! Press R to play again, or Esc to select level.

==============
WINCONDITIONS
==============

//all cell on lit

=======
LEVELS
=======

message Left click to draw horizontal line, right click vertical

message The aim is to light up all the boxes

level Small 7x7 puzzle
***********
*.........*
*.3-----4.*
*.-------.*
*.3-5-2-4.*
*.-------.*
*.3-6---5.*
*.-------.*
*.4-3-2-4.*
*.........*
***********

level Medium 10x10 puzzle
**************
*............*
*.-2--3-2-4-.*
*.3--1-----2.*
*.--2-5---4-.*
*.5--4-2----.*
*.----------.*
*.-1-3-4--3-.*
*.4---6----5.*
*.----------.*
*.2---4---1-.*
*.---2-3---3.*
*............*
**************

level Large 15x15 puzzle
*******************
*.................*
*.2-5-----------3.*
*.---1-4-5---5-2-.*
*.2---1----2-----.*
*.-------------2-.*
*.2----2---------.*
*.----4--4-4-----.*
*.--4---4----5-4-.*
*.-------1-2-----.*
*.----------1----.*
*.--4---6--3-----.*
*.-------------3-.*
*.---------------.*
*.--2---4-2----2-.*
*.----3-----2----.*
*.2--3-------4--3.*
*.................*
*******************
