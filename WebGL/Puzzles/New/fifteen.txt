title Fifteen
author polyomino games
homepage polyomino.org

comment //
endline ;

background_color lightbrown
text_color darkblue
run_rules_on_level_start
hover
noarrows

========
OBJECTS
========
Background .;lightblue
edge *;lightbrown
info +;lightbrown
player;transparent
N;red
W;lightbrown
X;lightbrown
Y;lightbrown
Z;lightbrown
lit;transparent

focus // for hover
lightgrey
000000000000
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
000000000000

grid
darkgrey
000000000000
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
0..........0
000000000000

0;transparent
1;blue;resize 0.8;text 1
2;blue;resize 0.8;text 2
3;blue;resize 0.7;text 3
4;blue;resize 0.7;text 4
5;blue;resize 0.7;text 5
6;blue;resize 0.7;text 6
7;blue;resize 0.7;text 7
8;blue;resize 0.7;text 8
9;blue;resize 0.7;text 9
A;blue;resize 0.7;text 10
B;blue;resize 0.7;text 11
C;blue;resize 0.7;text 12
D;blue;resize 0.7;text 13
E;blue;resize 0.7;text 14
F;blue;resize 0.7;text 15

tyou;blue;resize 0.5;text You
twin;blue;resize 0.5;text Win!

=======
LEGEND
=======

digit = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or A or B or C or D or E or F
clickable = 0 or digit
mark = W or X or Y or Z
P = player and edge

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
edge W X Y Z N grid info 
lit
0 digit player tyou twin
focus

======
RULES
======

[ action player ] -> reset
[ moving player ] -> [ player ]

[ tyou ] -> cancel

[ W | N | N | N | N ] -> [ W | N 1 | N 2 | N 3 | N 4 ]
[ X | N | N | N | N ] -> [ X | N 5 | N 6 | N 7 | N 8 ]
[ Y | N | N | N | N ] -> [ Y | N 9 | N A | N B | N C ]
[ Z | N | N | N | N ] -> [ Z | N D | N E | N F |   0 ]

(random [ N digit | 0 ] -> [ 0 | digit ] )

startloop
random [ N digit | 0 ] -> [ 0 | digit ] 
random [ N digit | | | 0 ] -> [ 0 | | | digit ] 
random [ N ] [ no N digit | 0 ] -> [ N ] [ 0 | digit ]
random [ N ] [ no N digit | | | 0 ] -> [ N ] [ 0 | | | digit ]
endloop

[ N ] -> [ ]

[ focus ] -> [ ]
[ hover clickable ] -> [ focus clickable ]

[ fire1 digit | 0 ] -> [ 0 | digit ]

[ grid ] -> [ ]
[ digit ] -> [ digit grid ]

[ lit ] -> [ ]
right [ W | 1 | 2 | 3 | 4 ] -> [ W lit | 1 | 2 | 3 | 4 ]
right [ X | 5 | 6 | 7 | 8 ] -> [ X lit | 5 | 6 | 7 | 8 ]
right [ Y | 9 | A | B | C ] -> [ Y lit | 9 | A | B | C ]
right [ Z | D | E | F | 0 ] -> [ Z lit | D | E | F | 0 ]

([ lit | lit | lit | lit ] [ info ] -> message You win!)
down [ lit | lit | lit | lit ] [ info ] -> [ lit | lit | lit | lit ] [ info tyou ]
right [ info tyou | info tyou ] -> [ info tyou | info twin ]

=======
LEVELS
=======

P*****
WNNNN*
XNNNN*
YNNNN*
ZNNNN*
**++**