title 2048
author polyomino games
homepage polyomino.org

comment //
endline ;

// This is an implementation of the 2048 game designed to show off Puzzlang extensions
// Text objects using TEXT 
// Icons of any size with definable scaling factor
// End of line comments using //
// Line breaks using \\

background_color blue
text_color yellow
run_rules_on_level_start
noaction
========
OBJECTS
========

Background .; green;
edge *      ; green;
spot @      ; green;

over ; transparent;
live ; transparent;
orig ; transparent;

empty; lightgrey  ;
bg1  ; lightbrown ;
bg2  ; yellow     ;
bg3  ; orange     ;
bg4  ; red        ;
score; transparent;

1; white; resize 0.9; text 2
2; white; resize 0.8; text 4
3; white; resize 0.8; text 8
4; white; resize 0.8; text 16
5; white; resize 0.7; text 32
6; white; resize 0.7; text 64
7; white; resize 0.6; text 128
8; white; resize 0.6; text 256
9; white; resize 0.6; text 512
a; white; resize 0.4; text 1024
b; white; resize 0.4; text 2048

border #; lightbrown yellow orange red
0123
3012
2301
1230

grid; grey
0000000000000000
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0..............0
0000000000000000

=======
LEGEND
=======

number = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or a or b 
player = number or spot
rx = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or a
bg = bg1 or bg2 or bg3 or bg4

x = grid and empty
S = edge and score

================
COLLISIONLAYERS
================
Background
edge border spot 
live bg over
orig
empty number
grid score

======
SOUNDS
======

======
RULES
======

// note: rules run at beginning, so >spot is used to filter 

// check if game over
[ over ] -> cancel 

// mark all tiles as live (combinable) and original position
[ > number ] -> [ > number live orig ]

// loop needed to just keep sliding
startloop
  // kludge because cannot flag objects directly
  [ > number live    | empty ] -> [ empty | > number live ]
  [ > number no live | empty ] -> [ empty | > number no live ]

  // tiles stop at the edge
  [ > number | edge ] -> [ number | edge ]
  // combine moving tile with stationary working back from edge if both uncombined
  [ > 1 live | stationary 1 live ] -> [ empty | 2 ]
  [ > 2 live | stationary 2 live ] -> [ empty | 3 ]
  [ > 3 live | stationary 3 live ] -> [ empty | 4 ]
  [ > 4 live | stationary 4 live ] -> [ empty | 5 ]
  [ > 5 live | stationary 5 live ] -> [ empty | 6 ]
  [ > 6 live | stationary 6 live ] -> [ empty | 7 ]
  [ > 7 live | stationary 7 live ] -> [ empty | 8 ]
  [ > 8 live | stationary 8 live ] -> [ empty | 9 ]
  [ > 9 live | stationary 9 live ] -> [ empty | a ]
  [ > a live | stationary a live ] -> [ empty | b ]
  // kludge to kill movement just once
  [ > number | stationary number ] -> [ > number | action number ]
  [ > number | action number ] -> [ number | number ]
endloop

[ > number ] -> [ number ]
[ live ] -> [ ]

// cancel if all mark on number
[ > spot ] -> [ > spot live]
[ live ] [ number no orig ] -> [ ] [ number ]
[ live ] [ orig no number ] -> [ ] [ ]
[ live ] -> cancel
[ orig ] -> [ ]

// random new tile 10:1 2 vs 4 (two on startup)
random [ empty ] -> [ live ]
random [ stationary spot ] [ empty ] -> [ spot ] [ live ]
[ live ] -> [ random 1 1 1 1 1 1 1 1 1 2 ]

// check for can move
[ spot ] -> [ spot live]
[ live ] [ empty ] -> [ ] [ empty ]
[ live ] [ 1 | 1 ] -> [ ] [ 1 | 1 ]
[ live ] [ 2 | 2 ] -> [ ] [ 2 | 2 ]
[ live ] [ 3 | 3 ] -> [ ] [ 3 | 3 ]
[ live ] [ 4 | 4 ] -> [ ] [ 4 | 4 ]
[ live ] [ 5 | 5 ] -> [ ] [ 5 | 5 ]
[ live ] [ 6 | 6 ] -> [ ] [ 6 | 6 ]
[ live ] [ 7 | 7 ] -> [ ] [ 7 | 7 ]
[ live ] [ 8 | 8 ] -> [ ] [ 8 | 8 ]
[ live ] [ 9 | 9 ] -> [ ] [ 9 | 9 ]
[ live ] [ a | a ] -> [ ] [ a | a ]
[ live ] -> [ over ] message Game Over!

// now add backgrounds
[ bg ] -> [ ]
[ 1 ] -> [ grid bg1 1 ]
[ 2 ] -> [ grid bg1 2 ]
[ 3 ] -> [ grid bg2 3 ]
[ 4 ] -> [ grid bg2 4 ]
[ 5 ] -> [ grid bg2 5 ]
[ 6 ] -> [ grid bg3 6 ]
[ 7 ] -> [ grid bg3 7 ]
[ 8 ] -> [ grid bg3 8 ]
[ 9 ] -> [ grid bg4 9 ]
[ a ] -> [ grid bg4 a ]
[ b ] -> [ grid bg4 b ]

==============
WINCONDITIONS
==============

some b
 
=======
LEVELS
=======

message Regular 4x4 puzzle

#@***S*#
#*xxxx*#
#*xxxx*#
#*xxxx*#
#*xxxx*#
#******#

message Test 4x4 puzzle

#@***S*#
#*11xx*#
#*xxxx*#
#*xxxx*#
#*xxxx*#
#******#

message Test 4x4 puzzle

#@***S*#
#*x3x3*#
#*2424*#
#*1313*#
#*2424*#
#******#

message Small 3x3 puzzle

#@**S*#
#*xxx*#
#*xxx*#
#*xxx*#
#*****#

message Large 5x5 puzzle

#@******#
#*xxxxx*#
#*xxxxx*#
#*xxxxx*#
#*xxxxx*#
#*xxxxx*#
#*******#

